# position: 4
PREFIX premis: <http://www.loc.gov/premis/rdf/v3/>
PREFIX schema: <https://schema.org/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX haObj: <https://data.hetarchief.be/ns/object/> 
PREFIX haDes: <https://data.hetarchief.be/ns/description/> 
PREFIX haLicId: <https://data.hetarchief.be/id/license/>
PREFIX rel: <http://id.loc.gov/vocabulary/preservation/relationshipSubType/> 
PREFIX ebucore: <http://www.ebu.ch/metadata/ontologies/ebucore/ebucore#> 
prefix prov: <http://www.w3.org/ns/prov#>
# EBUCore - the Dublin Core for media
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> # XML Schema Definition
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT
?intellectual_entity_id 
?type
?collection_id 
?created_at
?updated_at
WHERE {
  ?intellectual_entity_id schema:isPartOf ?collection_id . 
  
  # Do not include images
  ?intellectual_entity_id dct:format ?format.
  VALUES ?format {
    #"image"
    "video"
    "videofragment"
    "audiofragment"
    "audio"
    "film"
    "newspaperpage"
    "newspaper"
  } 
  # Only include the right licenses
  FILTER EXISTS {
    ?intellectual_entity_id schema:license ?schema_license
    VALUES ?schema_license { 
        haLicId:VIAA-PUBLIEK-METADATA-LTD 
        haLicId:VIAA-PUBLIEK-METADATA-ALL
        haLicId:VIAA-PUBLIEK-CONTENT
        haLicId:BEZOEKERTOOL-METADATA-ALL
        haLicId:BEZOEKERTOOL-CONTENT 
        haLicId:VIAA-INTRA_CP-METADATA-ALL 
        haLicId:VIAA-INTRA_CP-CONTENT 
        haLicId:Publiek-Domein
        haLicId:COPYRIGHT-UNDETERMINED
    }
  }

  ?intellectual_entity_id prov:wasDerivedFrom/schema:dateModified ?modified .
  FILTER (!isLiteral(?since) || ?modified >= ?since )
  {
    ?collection_id a schema:Newspaper.
    BIND ("newspaper" AS ?type)
  }
  UNION {
    ?collection_id a schema:Episode.
    BIND ("episode" AS ?type)
  }
  UNION {
    ?collection_id a schema:CreativeWorkSeries.
    BIND ("series" AS ?type)
  }
  UNION {
    ?collection_id a schema:CreativeWorkSeason.
    BIND ("season" AS ?type)
  }
  UNION {
    ?collection_id a schema:ArchiveComponent.
    BIND ("archive" AS ?type)
  }
}
